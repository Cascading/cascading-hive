/*
 * Copyright (c) 2007-2015 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.text.SimpleDateFormat

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'provided-base'


ext.hadoopVersion = '2.5.1'
ext.hiveVersion = '1.0.0'

group = "cascading"
version = "1.0"

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}

ext.timestamp = new SimpleDateFormat( "yyyyMMdd" ).format( new Date() )

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
  }
}

configurations {
  testArtifacts {
    extendsFrom testRuntime
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven{ url 'http://conjars.org/repo/' }
  maven{ url 'http://www.datanucleus.org/downloads/maven2/' }
}

dependencies {
  // broken hive deps..
  compile group: 'sqlline', name: 'sqlline',  version: '1.1.6'
  compile group: 'cascading', name: 'cascading-hive', version: '2.1.0-wip-dev'
  compile group: 'com.hotels', name: 'corc-cascading', version: '1.0.0'

  compile( group: 'org.apache.hive', name: 'hive-jdbc', version: hiveVersion  ){
    exclude group: 'sqline'
    exclude group: 'org.slf4j'
  }
  compile( group: 'org.apache.hive.hcatalog', name: 'hive-webhcat-java-client', version: hiveVersion ) {
    exclude group: 'org.slf4j'
  }
  // add the line below, if you use hive 0.10
  //compile group: 'org.apache.hive', name: 'hive-builtins', version: hiveVersion

  provided group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoopVersion
  provided group: 'org.apache.hadoop', name: 'hadoop-mapreduce-client-core', version: hadoopVersion
  provided group: 'org.slf4j', name: 'slf4j-api', version: '1.7.6'
  provided group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.6'
  provided group: 'log4j', name: 'log4j', version: '1.2.17'

}

jar {
  description = "Assembles a Hadoop ready jar file"
  manifest {
    attributes( "Build-Date": "${timestamp}" )
  }

  doFirst {
    into( 'lib' ) {
      from configurations.compile
    }
  }
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

eclipse {
  classpath {
    defaultOutputDir = file( 'build' )
    downloadSources = true
    downloadJavadoc = true
  }
}
